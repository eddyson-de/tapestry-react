plugins {
    id "com.jfrog.bintray" version "1.4"
    id "com.moowork.node" version "0.11"
}

description = "Use React together with Tapestry"

group = "de.eddyson"
version = "0.3.0"

apply plugin: 'groovy'
apply plugin: 'maven'


def versions= [
  react: '0.14.1',
  webjars: '0.8.0',
  tapestry: '5.4-rc-1',
  slf4j: '1.7.12',
  
  // test scopes
  spock: '1.0-groovy-2.4',
  selenium: '2.48.2'
  
]

repositories {
  jcenter()
}

sourceCompatibility = '1.7'

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives sourceJar
}

dependencies {
  compile "org.slf4j:slf4j-api:$versions.slf4j"
  compile "org.apache.tapestry:tapestry-core:$versions.tapestry"
  compile "de.eddyson:tapestry-webjars:$versions.webjars"
  compile "org.apache.tapestry:tapestry-webresources:$versions.tapestry"
  
  runtime "org.webjars.bower:react:$versions.react"

  testCompile "javax.servlet:servlet-api:2.5"
  testCompile "org.spockframework:spock-tapestry:$versions.spock"
  testCompile 'de.eddyson:tapestry-geb:0.21.0'
  testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$versions.selenium"
  
  testRuntime "org.slf4j:slf4j-simple:$versions.slf4j"
  testRuntime "cglib:cglib-nodep:3.2.0"
  testRuntime 'org.objenesis:objenesis:2.2'
}

task installCoffeeScript(type: NpmTask) {
  def coffeeScriptVersion = '1.10.0'
  args = ['install', "coffee-script@$coffeeScriptVersion", '--save-dev']
}

task compileCoffeeScript(type: NodeTask, dependsOn: installCoffeeScript) {
  script = file('node_modules/.bin/coffee')
  args = [ "-c", "-o", "${project.buildDir}/compiled-coffeescript", 'src/main/resources/META-INF/modules' ]
}

jar {
  dependsOn compileCoffeeScript
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.react.ReactModule' }
  rootSpec.exclude 'META-INF/modules/**/*.coffee'
  from("${project.buildDir}/compiled-coffeescript"){ into "META-INF/modules" }
}

tasks.withType(Test){
  systemProperties["tapestry.service-reloading-enabled"] = "false"
  systemProperties["tapestry.execution-mode"] = "test"
  systemProperty 'webappLocation', 'src/test/webapp'
  systemProperty 'jettyPort', 9040
  enableAssertions = true
}

task runTestApp(type:JavaExec) {
  main = 'org.apache.tapestry5.test.Jetty7Runner'
  args "-d", "src/test/webapp/", "-p", "9040"
  systemProperties["tapestry.execution-mode"] = "test"
  classpath = configurations.testRuntime + sourceSets.test.output + sourceSets.main.output
}

bintray {
    user = project.properties.bintrayUser
    key = project.properties.bintrayAPIKey
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'de.eddyson:tapestry-react'
    }
}
