plugins {
    id "com.jfrog.bintray" version "1.8.0"
    id "com.moowork.node" version "1.2.0"
}

description = "Use React together with Tapestry"

group = "de.eddyson"
version = "0.32.2"

apply plugin: 'groovy'
apply plugin: 'maven'


def versions= [
  tapestry: '5.4.3',
  slf4j: '1.7.25',
  
  // test scopes
  spock: '1.1-groovy-2.4',
  selenium: '2.53.1'
  
]

repositories {
  jcenter()
}

sourceCompatibility = '1.7'

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives sourceJar
}

dependencies {
  compile "org.slf4j:slf4j-api:$versions.slf4j"
  compile "org.apache.tapestry:tapestry-core:$versions.tapestry"
  compile ("org.apache.tapestry:tapestry-webresources:$versions.tapestry"){
    exclude group:'com.google.javascript', module: 'closure-compiler'
    exclude group:'com.github.sommeri', module: 'less4j'
  }
  compile "commons-io:commons-io:2.6"

  testCompile "javax.servlet:servlet-api:2.5"
  testCompile "org.spockframework:spock-tapestry:$versions.spock"
  testCompile 'de.eddyson:tapestry-geb:0.29.0'
  testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$versions.selenium"
  testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$versions.selenium"
  testCompile "org.apache.tapestry:tapestry-webresources:$versions.tapestry"
  testCompile "io.github.bonigarcia:webdrivermanager:1.7.2"

  testRuntime "org.slf4j:slf4j-simple:$versions.slf4j"
}

def compiledCoffeeScriptDir = "${project.buildDir}/compiled-coffeescript"

task compileCoffeeScript(type: NodeTask, dependsOn: npmInstall) {
  inputs.files fileTree(dir: 'src/main/resources/META-INF/modules', include: '**/*.coffee')
  outputs.dir compiledCoffeeScriptDir

  script = file('node_modules/.bin/coffee')
  args = [ "-c", "-o", compiledCoffeeScriptDir, 'src/main/resources/META-INF/modules' ]
  doFirst {
    delete compiledCoffeeScriptDir
  }
}

processResources {
  dependsOn npmInstall
  from('node_modules/react/umd'){
    into 'de/eddyson/tapestry/react/services'
  }
  from('node_modules/react-dom/umd'){
    into 'de/eddyson/tapestry/react/services'
  }
  from('node_modules/prop-types'){
    include 'prop-types*.js'
    into 'de/eddyson/tapestry/react/services'
  }
  from('node_modules/babel-standalone'){
    include 'babel.min.js'
    into 'de/eddyson/tapestry/react/services'
  }
}

jar {
  dependsOn compileCoffeeScript
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.react.modules.ReactModule' }
  rootSpec.exclude 'META-INF/modules/**/*.coffee'
  from(compiledCoffeeScriptDir){ into "META-INF/modules" }
}

tasks.withType(Test){
  systemProperty "geb.env", System.getProperty("geb.env") ?: 'firefox'
  systemProperties["tapestry.service-reloading-enabled"] = "false"
  systemProperties["tapestry.execution-mode"] = "test"
  systemProperty 'webappLocation', 'src/test/webapp'
  systemProperty 'jettyPort', 9040
  enableAssertions = true
  testLogging {
    exceptionFormat "full"
  }
}

task runTestApp(type:JavaExec) {
  main = 'org.apache.tapestry5.test.Jetty7Runner'
  args "-d", "src/test/webapp/", "-p", "9040"
  systemProperties["tapestry.execution-mode"] = "test"
  classpath = configurations.testRuntime + sourceSets.test.output + sourceSets.main.output
}

clean {
  delete 'node_modules'
}

bintray {
    user = 'fooberger'
    key = System.env.bintrayAPIKey
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'de.eddyson:tapestry-react'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/eddyson-de/tapestry-react'
    }
}
