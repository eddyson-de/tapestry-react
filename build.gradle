plugins {
    id "com.jfrog.bintray" version "1.4"
    id "com.moowork.node" version "0.11"
}

description = "Use React together with Tapestry"

group = "de.eddyson"
version = "0.2.3"

apply plugin: 'groovy'
apply plugin: 'maven'


def versions= [
  react: '0.14.0',
  webjars: '0.8.0',
  tapestry: '5.4-beta-35',
  slf4j: '1.7.12',
  
  // test scopes
  spock: '1.0-groovy-2.4'
]

repositories {
  jcenter()
}

sourceCompatibility = '1.7'

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

artifacts {
  archives sourceJar
}

dependencies {
  compile "org.slf4j:slf4j-api:$versions.slf4j"
  compile "org.apache.tapestry:tapestry-core:$versions.tapestry"
  compile "de.eddyson:tapestry-webjars:$versions.webjars"
  compile "org.apache.tapestry:tapestry-webresources:$versions.tapestry"
  
  runtime "org.webjars:react:$versions.react"

  testCompile "javax.servlet:servlet-api:2.5"
  testCompile "org.spockframework:spock-tapestry:$versions.spock"
  testCompile "org.apache.tapestry:tapestry-test-constants:$versions.tapestry"
  
  
  testRuntime "org.slf4j:slf4j-simple:$versions.slf4j"
  testRuntime "cglib:cglib-nodep:3.1"
  testRuntime 'org.objenesis:objenesis:2.2'
}

task installCoffeeScript(type: NpmTask) {
  def coffeeScriptVersion = '1.10.0'
  args = ['install', "coffee-script@$coffeeScriptVersion", '--save-dev']
}

task compileCoffeeScript(type: NodeTask, dependsOn: installCoffeeScript) {
  script = file('node_modules/.bin/coffee')
  args = [ "-c", "-o", "${project.buildDir}/compiled-coffeescript", 'src/main/resources/META-INF/modules' ]
}

jar {
  dependsOn compileCoffeeScript
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.react.ReactModule' }
  rootSpec.exclude 'META-INF/modules/**/*.coffee'
  from("${project.buildDir}/compiled-coffeescript"){ into "META-INF/modules" }
}

bintray {
    user = project.properties.bintrayUser
    key = project.properties.bintrayAPIKey
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'de.eddyson:tapestry-react'
    }
}
